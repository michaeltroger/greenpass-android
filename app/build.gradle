plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'de.mannodermaus.android-junit5'
}

def keystoreProperties = new Properties()
try {
    def keystorePropertiesFile = rootProject.file("keystore.properties");
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch(IOException ignored) {
    // We don't have release keys, ignoring
}

android {
    signingConfigs {
        release {
            if (keystoreProperties['STORE_FILE']) {
                storeFile rootProject.file(keystoreProperties['STORE_FILE'])
                storePassword keystoreProperties['STORE_PASSWORD']
                keyAlias keystoreProperties['KEY_ALIAS']
                keyPassword keystoreProperties['KEY_PASSWORD']
            }
        }
    }
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.michaeltroger.gruenerpass"
        minSdk 21
        targetSdk 30
        versionCode 27
        versionName "1.54"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.all {
            reports {
                junitXml.enabled = true
                junitXml.destination = file("$buildDir/../../build/test-results")
            }
        }
    }

}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha02'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0-rc01'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.68'
    implementation fileTree(dir: "libs", include: ["*.aar"])

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1'
    testImplementation 'org.assertj:assertj-core:3.20.2'
    testImplementation 'app.cash.turbine:turbine:0.5.2'

    testImplementation 'io.mockk:mockk:1.12.0'
}
