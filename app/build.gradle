plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def keystoreProperties = new Properties()
try {
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch(IOException ignored) {
    // We don't have release keys, ignoring
}

android {
    signingConfigs {
        release {
            if (keystoreProperties['STORE_FILE']) {
                storeFile rootProject.file(keystoreProperties['STORE_FILE'])
                storePassword keystoreProperties['STORE_PASSWORD']
                keyAlias keystoreProperties['KEY_ALIAS']
                keyPassword keystoreProperties['KEY_PASSWORD']
            }
        }
    }
    compileSdk 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.michaeltroger.gruenerpass"
        minSdk 21
        targetSdk 33
        versionCode 40
        versionName "2.3.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += [
                '-Xopt-in=kotlin.RequiresOptIn'
        ]
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                reports {
                    junitXml.enabled = true
                    junitXml.destination = file("$buildDir/../../build/test-results")
                }
            }
        }
    }
    namespace 'com.michaeltroger.gruenerpass'
}

ext {
    room_version = "2.4.3"
    lifecycle_version = "2.5.1"
    groupie_version = "2.9.0"
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.4'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation "com.github.lisawray.groupie:groupie:$groupie_version"
    implementation "com.github.lisawray.groupie:groupie-viewbinding:$groupie_version"
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.google.zxing:core:3.3.3' // last version to support API < 24
    implementation 'com.tom-roush:pdfbox-android:2.0.25.0'
}
